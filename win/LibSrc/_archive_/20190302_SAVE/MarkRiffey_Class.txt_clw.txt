re: our debug class discussion the other day: 

The .clw 


         MAP
           MODULE('Win32') 
RUC_ODS(*CSTRING),PASCAL,RAW,NAME('OutputDebugStringA')
           END 
         END

!------------------------------------------------------------------------------
RifUtilClass.Debug             PROCEDURE(STRING pMessage)
!------------------------------------------------------------------------------
csMessage          CSTRING(1000)
 CODE 

 csMessage = pMessage
 SELF.Debug(csMessage)
 RETURN 

 
!------------------------------------------------------------------------------
RifUtilClass.Debug             PROCEDURE(*CSTRING pMessage)
!------------------------------------------------------------------------------
csLocalMsg   CSTRING(1010) 
 CODE 
 
 IF SELF.DebugOff = TRUE 
    RETURN
 END
 
 csLocalMsg = pMessage & '<13,10,0>'

 RUC_ODS(csLocalMsg)
 RETURN 

!------------------------------------------------------------------------------ 
RifUtilClass.SetDebugOff PROCEDURE() 
!------------------------------------------------------------------------------
 CODE
 
 SELF.DebugOff = TRUE 
 
 RETURN

!------------------------------------------------------------------------------
RifUtilClass.SetDebugOn PROCEDURE() 
!------------------------------------------------------------------------------

 CODE

 SELF.DebugOff = FALSE 
 
 RETURN 

!----------------------------------------------------------------------------------------------------------------
RifUtilClass.EnableTraceForTable      PROCEDURE(*FILE pTable)
!----------------------------------------------------------------------------------------------------------------
 CODE

 IF EXISTS(CLIP(PATH()) & '\trace.txt') = TRUE
 ELSE
    RETURN
 END 
 
 IF SYSTEM{PROP:DriverTracing} = '1'
 ELSE
    SYSTEM{PROP:DriverTracing} = '1'
 END 

 SELF.TablesBeingTraced += 1
 
 pTable{PROP:TraceFile} = 'DEBUG:'
 pTable{PROP:Details}   = 1 
 pTable{PROP:Profile}   = 'DEBUG:' 
 pTable{PROP:LogSQL}    = 1
 
 RETURN 

 !----------------------------------------------------------------------------------------------------------------
RifUtilClass.DisableTraceForTable      PROCEDURE(*FILE pTable)
!----------------------------------------------------------------------------------------------------------------
 CODE
 
 SELF.TablesBeingTraced -= 1
 
 IF SELF.TablesBeingTraced = 0 
    SYSTEM{PROP:DriverTracing} = ''
 END 
 
 pTable{PROP:TraceFile} = ''
 pTable{PROP:Details}   = 0 
 pTable{PROP:Profile}   = '' 
 pTable{PROP:LogSQL}    = 0
 
 RETURN 


The .inc: 


        OMIT('_EndOfInclude_',_IFDef_RifUtil_)
_IFDef_RifUtil_            EQUATE(1)
!
!--------------------------------------------------------------------------------
RifUtilClass    CLASS,TYPE,THREAD,MODULE('clsRifUtil.CLW'),LINK('clsRifUtil.CLW',_ABCLinkMode_),DLL(_ABCDllMode_)
!
!--------------------------------------------------------------------------------
!Properties
!----------
DebugOff                LONG
TablesBeingTraced       LONG
!--------
! Methods
!-----------
SetDebugOff                     PROCEDURE()  
SetDebugOn                      PROCEDURE()
Debug                           PROCEDURE(STRING pMessage)
Debug                           PROCEDURE(*CSTRING pMessage)
EnableTraceForTable             PROCEDURE(*FILE pTable)
DisableTraceForTable            PROCEDURE(*FILE pTable)
                        END 
        _EndOfInclude_
