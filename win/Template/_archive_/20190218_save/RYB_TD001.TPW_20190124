#!-----------------------------------------------------------------------------------------------------
#!-----------------------------------------------------------------------------------------------------
#!-----------------------------------------------------------------------------------------------------
#!-----------------------------------------------------------------------------------------------------
#GROUP(%PrepareVariablesTD)
#PREPARE
#!
#IF(NOT VAREXISTS(%NextLine0))
  #DECLARE(%NextLine0)
  #DECLARE(%NextLine1)
  #DECLARE(%NextLine2)
  #DECLARE(%NextLine3)
#ENDIF
#!
#ENDPREPARE
#!-----------------------------------------------------------------------------------------------------
#GROUP(%GenATSTART)
#DECLARE(%TDtplDataExternal)
#IF(%TDtplMultiDLL=1 AND %TDtplMultiDLLData=0)
  #!SET(%TDtplDataExternal,',EXTERNAL,DLL')
  #SET(%TDtplDataExternal,',EXTERNAL,DLL(dll_mode)')
#ENDIF
#!-----------------------------------------------------------------------------------------------------
#GROUP(%GenATEND)
#!-----------------------------------------------------------------------------------------------------
#GROUP(%GenAfterGlobalIncludes)
INCLUDE('UDRA_UltimateDebug.INC'),ONCE #<! Thread Debug Classes
#!-----------------------------------------------------------------------------------------------------
#GROUP(%GenCustomGlobalDeclarations)
#INSERT(%FamilyFind, %FamilyFound)
#IF(%FamilyFound='LEGACY')
  #PROJECT('UDRA_UltimateDebug.CLW')
#ENDIF
#!-----------------------------------------------------------------------------------------------------
#!-----------------------------------------------------------------------------------------------------
#GROUP(%GenGlobalData)
TD_TplVersion %[25]NULL CSTRING('%TDtplVersion')%TDtplDataExternal
TD_TplRelease %[25]NULL CSTRING('%TDtplRelease')%TDtplDataExternal
  #IF(%TDtplClassGlobal1)
%[39]TDtplClassGlobal1Name CLASS(UltimateDebug)%TDtplDataExternal
%[39]NULL END
  #ENDIF
  #IF(%TDtplClassGlobal2)
%[39]TDtplClassGlobal2Name CLASS(UltimateDebug)%TDtplDataExternal
%[39]NULL END
  #ENDIF
  #IF(%TDtplClassGlobal3)
%[39]TDtplClassGlobal3Name CLASS(UltimateDebug)%TDtplDataExternal
%[39]NULL END
  #ENDIF
#!-----------------------------------------------------------------------------------------------------
#GROUP(%GenDLLExportList)

;BEGIN: Exporting from ThreadDebug classes
$TD_TplVersion %[40]NULL @?  ;Template Version Number
$TD_TplRelease %[40]NULL @?  ;Template Version Release Date
  #IF(%TDtplClassGlobal1)
$%[54]TDtplClassGlobal1Name @?  ;Global Class Name 1
  #ENDIF
  #IF(%TDtplClassGlobal2)
$%[54]TDtplClassGlobal2Name @?  ;Global Class Name 2
  #ENDIF
  #IF(%TDtplClassGlobal3)
$%[54]TDtplClassGlobal3Name @?  ;Global Class Name 3
  #ENDIF
;ENDED: Exporting from ThreadDebug classes

  #!$%[40]CLSkelClassGlobal3Name @?
#!-----------------------------------------------------------------------------------------------------
#!-----------------------------------------------------------------------------------------------------
#GROUP(%GenProgramSetup)
  #IF(%TDtplClassGlobal1)
%TDtplClassGlobal1Name.Init() #<! Thread Debug Global Class Name 1
%TDtplClassGlobal1Name.DebugPrefix   = '%TDtplPrefix'
%TDtplClassGlobal1Name.ASCIIFileName = '%TDtplLogFileName'
  #ENDIF
  #IF(%TDtplClassGlobal2)
%TDtplClassGlobal2Name.Init() #<! Thread Debug Global Class Name 2
%TDtplClassGlobal2Name.DebugPrefix   = '%TDtplPrefix'
%TDtplClassGlobal2Name.ASCIIFileName = '%TDtplLogFileName'
  #ENDIF
  #IF(%TDtplClassGlobal3)
%TDtplClassGlobal3Name.Init() #<! Thread Debug Global Class Name 2
%TDtplClassGlobal3Name.DebugPrefix   = '%TDtplPrefix'
%TDtplClassGlobal3Name.ASCIIFileName = '%TDtplLogFileName'
  #ENDIF
#!-----------------------------------------------------------------------------------------------------
#GROUP(%GenProgramEnd)
  #IF(%TDtplClassGlobal1)
%TDtplClassGlobal1Name.Kill() #<! Thread Debug Global Class Name 1
  #ENDIF
  #IF(%TDtplClassGlobal2)
%TDtplClassGlobal2Name.Kill() #<! Thread Debug Global Class Name 2
  #ENDIF
  #IF(%TDtplClassGlobal3)
%TDtplClassGlobal3Name.Kill() #<! Thread Debug Global Class Name 2
  #ENDIF
#!-----------------------------------------------------------------------------------------------------
#!-----------------------------------------------------------------------------------------------------
#GROUP(%GenProgramSetupDumpInfo)
#COMMENT(90)
  #IF(%TDtplDumpGlobalInformation AND ~%TDtplDisable)
DumpGlobalInformation_%Application()      #<! Dump Thread Debug GLOBAL information
  #ENDIF
  #IF(%TDtplDumpGlobalVariables AND ~%TDtplDisable)
DumpGlobalVariables_%Application()        #<! Dump Thread Debug GLOBAL variables
  #ENDIF
#COMMENT(60)
#!-----------------------------------------------------------------------------------------------------
#GROUP(%GenGlobalMap)
  #IF(NOT VAREXISTS(%NextLine0))
    #DECLARE(%NextLine0)
    #DECLARE(%NextLine1)
    #DECLARE(%NextLine2)
    #DECLARE(%NextLine3)
  #ENDIF
  #INDENT(-5)

  #COMMENT(90)
  #SET(%NextLine0, 'DumpGlobalInformation_' & %Application)
%[39]NextLine0 PROCEDURE() #<! Thread Debug Prototype
  #SET(%NextLine0, 'DumpGlobalVariables_' & %Application)
%[39]NextLine0 PROCEDURE() #<! Thread Debug Prototype
  #COMMENT(60)

  #INDENT(+5)
#!-----------------------------------------------------------------------------------------------------
#GROUP(%GenProgramProcedures)
  #IF(NOT VAREXISTS(%NextLine0))
    #DECLARE(%NextLine0)
    #DECLARE(%NextLine1)
    #DECLARE(%NextLine2)
    #DECLARE(%NextLine3)
  #ENDIF

!BOE: Thread Global Dump Information
  #SET(%NextLine0, 'DumpGlobalInformation_' & %Application)
%[39]NextLine0 PROCEDURE()
  CODE
  #IF(%TDtplGenerateGlobalInformation AND ~%TDtplDisable)
  %TDtplClassGlobal1Name.Debug('----------------> APPLICATION INFORMATION')
  %TDtplClassGlobal1Name.Debug('Information Generated on: ' & FORMAT(TODAY(),@D010) & ' - ' & FORMAT(CLOCK(),@T04))
    #IF(%ProgramExtension = 'EXE')
  %TDtplClassGlobal1Name.Debug('CW Version: Lib ' & SYSTEM{PROP:LibVersion} & ' Exe ' & SYSTEM{PROP:ExeVersion} & '')
    #ENDIF
  %TDtplClassGlobal1Name.Debug('Application Name: %Application ')
    #IF (%ApplicationDebug = %True)
  %TDtplClassGlobal1Name.Debug('Compiled in DEBUG mode.')
    #ENDIF
    #IF (%ApplicationLocalLibrary = %TRUE)
  %TDtplClassGlobal1Name.Debug('Compiled with LOCAL option.')
    #ENDIF
    #IF (%Target32 = %True)
  %TDtplClassGlobal1Name.Debug('Application is 32 bits.')
    #ELSE
  %TDtplClassGlobal1Name.Debug('Application is 16 bits.')
    #ENDIF
  %TDtplClassGlobal1Name.Debug('First procedure: %FirstProcedure')
  %TDtplClassGlobal1Name.Debug('Program Extension: %ProgramExtension')
  %TDtplClassGlobal1Name.Debug('Dictionary Name: ' & UPPER('%DictionaryFile'))
  %TDtplClassGlobal1Name.Debug('Installation Path: ' & LONGPATH(PATH()))
  %TDtplClassGlobal1Name.Debug('----------------> ')
  #INSERT(%GenDumpGlobalTemplates)
  #ENDIF
  RETURN
#!-----------------------------------------------------------------------------------------------------
  #SET(%NextLine0, 'DumpGlobalVariables_' & %Application)
%[39]NextLine0 PROCEDURE()
  CODE
  #IF(NOT VAREXISTS(%Prefix))
    #DECLARE (%Prefix)
    #DECLARE (%VarName)
    #DECLARE (%PrefixStart)
    #DECLARE (%PrefixEnd)
    #DECLARE (%DataStmt)
  #ENDIF
  #IF(%TDtplGenerateGlobalVariables AND ~%TDtplDisable)
  %TDtplClassGlobal1Name.Debug('----------------> GLOBAL VARIABLES')
    #FOR(%GlobalData)
      #SET(%DataStmt,QUOTE(%GlobalDataStatement))
#! ! ===>>>%DataStmt<<<===
#! ! ===>>> %GlobalDataID | %GlobalDataFull | %GlobalDataInDictionary | %GlobalDataStatement | %GlobalDataLast | %GlobalDataLevel | %GlobalDataUserOptions
      #IF (INSTRING('QUEUE',%GlobalDataStatement,1,1) OR INSTRING('GROUP',%GlobalDataStatement,1,1))
        #SET(%PrefixStart,INSTRING('PRE(',%GlobalDataStatement,1,1) + LEN('PRE('))
        #SET(%PrefixEnd  ,INSTRING(')',%GlobalDataStatement,1,%PrefixStart))
        #IF (%PrefixStart)
          #SET(%Prefix,SUB(%GlobalDataStatement, %PrefixStart, %PrefixEnd-%PrefixStart) & ':')
          #IF (LEN(%Prefix) = 1)
            #SET(%Prefix,'')
          #ENDIF
        #ENDIF
  %TDtplClassGlobal1Name.Debug('Only the active record of a group or queue is displayed.')
#!  %TDtplClassGlobal1Name.Debug('Global data: %[23]GlobalData %[17]DataStmt')  ! & ' Records: ' & RECORDS(%GlobalData))
#!  %TDtplClassGlobal1Name.Debug('Global data: %[23]GlobalData %[17]DataStmt')
  %TDtplClassGlobal1Name.Debug('Global data: %[23]GlobalData %DataStmt')
      #ELSE
        #IF (INSTRING('END',%GlobalDataStatement,1,1) OR INSTRING('FILE',%GlobalDataStatement,1,1))
          #SET (%Prefix,'')
#!  %TDtplClassGlobal1Name.Debug('Global Data: %[23]GlobalData %[17]DataStmt')
  %TDtplClassGlobal1Name.Debug('Global Data: %[23]GlobalData %DataStmt')
        #ELSE
          #! RA.2014.04.19 - No ARRAYS or REFERENCES are allowed or supported.
          #IF(INSTRING(',DIM',UPPER(%DataStmt))>0)
  %TDtplClassGlobal1Name.Debug('Global Data: %[23]GlobalData is an ARRAY variable and NOT SUPPORTED.')
          #ELSIF(INSTRING('&',UPPER(%DataStmt))>0) #! RA.2014.04.27 - Reference variable
  %TDtplClassGlobal1Name.Debug('Global Data: %[23]GlobalData is a REFERENCE variable and NOT SUPPORTED.')
          #ELSE
            #! RA.2019.01.04.FRI: Use the VARDUMP(NO) For anything that is not handled. Like CLASS objects.
            #SET(%PrefixStart,INSTRING('VARDUMP(',%GlobalDataUserOptions,1,1) + LEN('VARDUMP('))
            #SET(%PrefixEnd  ,INSTRING(')',%GlobalDataUserOptions,1,%PrefixStart))
            #SET(%VarName    ,UPPER(SUB(%GlobalDataUserOptions,%PrefixStart,%PrefixEnd-%PrefixStart)))
            #IF(SUB(%VarName,1,1) = 'N')
  %TDtplClassGlobal1Name.Debug('Global Data: %[23]GlobalData %DataStmt')
            #ELSE
  %TDtplClassGlobal1Name.Debug('Global Data: %[23]GlobalData %[17]DataStmt Value: ''' & CLIP(%Prefix%GlobalData) & '''')
            #END
          #ENDIF
          #! RA.2014.04.19 - No ARRAYS or REFERENCES are allowed or supported.
        #ENDIF
      #ENDIF
    #ENDFOR
  %TDtplClassGlobal1Name.Debug('---------------->')
  #ENDIF
#!
  RETURN
!EOE: Thread Global Dump Information

#!-----------------------------------------------------------------------------------------------------
#! RA.2019.01.05.SAT: Moved into their own group
#GROUP(%GenDumpGlobalTemplates)
#IF(%TDtplDumpGlobalTemplates AND ~%TDtplDisable)
  #IF(ITEMS(%ApplicationTemplate))
%TDtplClassGlobal1Name.Debug(':TEMPLATES:GLOBAL BEGIN %Application')
    #FOR(%ApplicationTemplate)
%TDtplClassGlobal1Name.Debug(':TEMPLATES:GLOBAL %ApplicationTemplate ')
    #ENDFOR
  #ENDIF
%TDtplClassGlobal1Name.Debug(':TEMPLATES:GLOBAL ENDED %Application')
#ENDIF
#!-----------------------------------------------------------------------------------------------------
#! RA.2019.01.05.SAT: Moved into their own group
#GROUP(%GenDumpLocalTemplates)
#IF(%TDtplDumpLocalTemplates AND ~%TDtplDisable)
  #IF(ITEMS(%ActiveTemplate))
%TDtplClassGlobal1Name.Debug(':TEMPLATES:LOCAL BEGIN %Procedure')
   #FOR(%ActiveTemplate)
%TDtplClassGlobal1Name.Debug(':TEMPLATES:LOCAL %[18]ActiveTemplateType %ActiveTemplate ')
   #ENDFOR
%TDtplClassGlobal1Name.Debug(':TEMPLATES:LOCAL ENDED %Procedure')
  #ENDIF
#ENDIF
#!-----------------------------------------------------------------------------------------------------
#!-----------------------------------------------------------------------------------------------------
#! RA.2019.01.07.MON: ABC: Declare the LOCAL class objects
#GROUP(%GenDataSection)
  #IF(NOT VAREXISTS(%ClassDefine))
    #DECLARE (%ClassDefine)
  #ENDIF
  #SET(%ClassDefine,'ThreadDebugLocal')
  #IF(%TDtplClassLocal1)
#EMBED(%TD_A_ClassObjects,'BEGIN: Thread Debug Local Definitions')
#! #<! Thread Debug Local Class Object Definitions
%[19]TDtplClassLocal1Name %ClassDefine #<! Thread Debug Local Class Name 1
  #ENDIF
  #IF(%TDtplClassLocal2)
%[19]TDtplClassLocal2Name %ClassDefine #<! Thread Debug Local Class Name 2
  #ENDIF
  #IF(%TDtplClassLocal3)
%[19]TDtplClassLocal3Name %ClassDefine #<! Thread Debug Local Class Name 3
  #ENDIF
#! #<! Thread Debug Local Class Object Definitions
#EMBED(%TD_Z_ClassObjects,'ENDED: Thread Debug Local Definitions')
#!-----------------------------------------------------------------------------------------------------
#! RA.2019.01.05.SAT: ABC: Do this around the .Run method
#GROUP(%GenBeginRUN)
#! TESTME: 0004: WHERE
#! RA.2019.01.05.SAT: The %FirstProcedure is when we show the GLOBAL templates
#IF(%FirstProcedure = %Procedure)
#INSERT(%GenDumpGlobalTemplates)
#ENDIF
%TDtplClassGlobal1Name.Enter('%Application','%Module','%Procedure') #<! %ProcedureTemplate
#IF(%TRUE)
#INSERT(%GenDumpLocalTemplates)
#ENDIF
#!-----------------------------------------------------------------------------------------------------
#! RA.2019.01.05.SAT: ABC: Do this around the .Run method
#GROUP(%GenEndedRUN)
#! TESTME: 9996: WHERE
%TDtplClassGlobal1Name.Exits('%Application','%Module','%Procedure') #<! %ProcedureTemplate
#!-----------------------------------------------------------------------------------------------------
#!-----------------------------------------------------------------------------------------------------
#!-----------------------------------------------------------------------------------------------------
#!-----------------------------------------------------------------------------------------------------
